pre createEvaluation {
	var evaluation = new CorrectionDefects!Evaluation;
	evaluation.guiName = UsabilityGUI!GUI.allInstances().first().guiElement.name;
	
	var autocorrect = true;
	var choices = Sequence{"Ask me what to correct", "Automatically correct everything"};
	var choice = System.user.choose("¿What do you want me to do?", choices, choices.get(0));
	
	switch (choice) {
		case choices.get(0) : autocorrect = false;
	}	
}

post evaluationDone {
	System.user.inform("Usability correction finalized. A report has been generated.");
}

operation String getR() : Integer {
	return self.substring(1,3).hexToInt();
}

operation String getG() : Integer {
	return self.substring(3,5).hexToInt();
}

operation String getB() : Integer {
	return self.substring(5,7).hexToInt();
}

operation String hexToInt() : Integer {
	var eqs = new Map;
	eqs.put("A", 10); eqs.put("a", 10);
	eqs.put("B", 11); eqs.put("b", 11);
	eqs.put("C", 12); eqs.put("c", 12);
	eqs.put("D", 13); eqs.put("d", 13);
	eqs.put("E", 14); eqs.put("e", 14);
	eqs.put("F", 15); eqs.put("f", 15);
	
	var chars = self.toCharSequence().invert();
	
	var number = new Integer;
	var result = 0;
	for (i in Sequence {0..chars.size()-1}) {
		var char = chars.get(i);
		if (eqs.containsKey(char))
			number = eqs.get(char);
		else
			number = char.asInteger();
			
		result = result + number * 16.pow(i);
	}

	return result.asInteger();
}

operation String getLuminance() : Real {
	var initialValues = Sequence{self.getR(), self.getG(), self.getB()};
	var finalValues = new Sequence;
	
	for (value : Integer in initialValues) {
		var finalValue = value.asReal()/255;
		if (finalValue <= 0.03928)
			finalValue = finalValue/12.92;
		else
			finalValue = ((finalValue+0.055)/1.055).pow(2.4);
			
		finalValues.add(finalValue);
	}
	
	return finalValues.first()*0.2126 + finalValues.second()*0.7152 + finalValues.third()*0.0722;
}


[% for (rule in Rule.allInstances()) { %]

[%if(rule.restriction.isDefined()) { %]
operation [%=rule.name.replace(' ', '_')%]_restriction(usabilityElement : UsabilityGUI![%=rule.elementType%]) {
	[%=rule.restriction.code%]
}
[% } %]

operation [%=rule.name.replace(' ', '_')%]_calculation(usabilityElement : UsabilityGUI![%=rule.elementType%])
	: [% switch (rule.range.type().name) { case "IntegerRange" : out.print("Integer"); case "DoubleRange" : out.print("Real"); case "BooleanRange" : out.print("Boolean"); case "StringRange" : out.print("String"); } %] {
	[%=rule.calculation.code%]
}

[%if(rule.correction.isDefined()) { %]
operation [%=rule.name.replace(' ', '_')%]_correction(usabilityElement : UsabilityGUI![%=rule.elementType%]) {
	[%=rule.correction.code%]
}
[% } %]

@greedy
rule [%=rule.name.replace(' ', '_')%]
    transform usabilityElement : UsabilityGUI![%=rule.elementType%]
	to defect : CorrectionDefects!Defect { 
	
	guard : [%if(rule.restriction.isDefined()) { %] [%=rule.name.replace(' ', '_')%]_restriction(usabilityElement) and [% } %] 
			not([%if (rule.range.isKindOf(IntegerRange)) { %]
[%=rule.name.replace(' ', '_')%]_calculation(usabilityElement) >= [%=rule.range.min%][%if (rule.range.max <> -1) { %] and [%=rule.name.replace(' ', '_')%]_calculation(usabilityElement) <= [%=rule.range.max%][% } %])
			[%} else if (rule.range.isKindOf(DoubleRange)) { %]
[%=rule.name.replace(' ', '_')%]_calculation(usabilityElement) >= [%=rule.range.min%][%if (rule.range.max <> -1) { %] and [%=rule.name.replace(' ', '_')%]_calculation(usabilityElement) <= [%=rule.range.max%][% } %])
			[%} else if (rule.range.isKindOf(BooleanRange)) { %]
[%=rule.name.replace(' ', '_')%]_calculation(usabilityElement) == [%=rule.range.correctValue%])
			[%} else if (rule.range.isKindOf(StringRange)) { %]
Sequence{[%=rule.range.correctValues%]}.includes([%=rule.name.replace(' ', '_')%]_calculation(usabilityElement)))
			[% } %]	
		defect.description = "[%=rule.description.errorMessage%]";
		defect.usabilityRule = "[%=rule.name%]";
		defect.element = usabilityElement.guiElement.name;
		
		[%if(rule.correction.isDefined()) { %]
		if (autocorrect or System.user.confirm(usabilityElement.guiElement.name + ": [%=rule.description.errorMessage%]. Do you want to correct this defect?", true)) {
			[%=rule.name.replace(' ', '_')%]_correction(usabilityElement);
			defect.corrected = true;
		}
		else {
			defect.corrected = false;
		}
		[% } else { %]
			defect.corrected = false;
		[% } %]
		
		evaluation.defects.add(defect);
	} 
	
	
[% } %]
